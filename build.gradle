plugins {
    id 'application'
    id "org.flywaydb.flyway" version "7.5.1"
    id "com.github.ben-manes.versions" version "0.27.0"
}

version = '0.1.4'

repositories {
    mavenCentral()
}

application {
    mainClass = 'com.cosmoport.core.App'
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

compileJava {
    options.compilerArgs << '-Xlint:-processing' << '-Xlint:all'
    options.warnings = true
    options.deprecation = true
    options.encoding = 'UTF-8'
}

distributions {
    main {
        contents {
            from(file("$projectDir/db")) {
                into 'db'
                include('**/*.db')
                includeEmptyDirs = false
            }
        }
    }
}

startScripts {
    applicationName = "core-$version"
}

distZip {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

ext {
    ver = [
            resteasy    : '4.0.0.Beta1',
            guice       : '5.1.0',
            jackson     : '2.9.3',
            sqlite      : '3.21.0.1',
            hikaricp    : '2.7.6',
            flyway      : '5.0.6',
            logback     : '1.2.3',
            slf4j       : '1.7.22',
            log4j       : '2.10.0',
            junit       : '5.1.0-M1',
            springJdbc  : '5.0.2.RELEASE',
            jetty       : '9.4.8.v20171121',
            jbcrypt     : '0.4',
            iocommons   : '2.6',
            websocketApi: '1.1'
    ]
}

test {
    useJUnitPlatform()
}

dependencies {
    // Server
    implementation "org.eclipse.jetty:jetty-servlet:$ver.jetty"

    implementation "org.eclipse.jetty.websocket:websocket-api:$ver.jetty"
    implementation "org.eclipse.jetty.websocket:websocket-server:$ver.jetty"
    implementation "org.eclipse.jetty.websocket:websocket-client:$ver.jetty"
    implementation "javax.websocket:javax.websocket-api:$ver.websocketApi"

    // JAX-RS
    implementation "org.jboss.resteasy:resteasy-jaxrs:$ver.resteasy"
    implementation("org.jboss.resteasy:resteasy-guice:$ver.resteasy") {
        exclude group: "org.jboss.resteasy", module: "resteasy-client"
    }
    implementation("org.jboss.resteasy:resteasy-multipart-provider:$ver.resteasy") {
        exclude group: "org.jboss.resteasy", module: "resteasy-jaxb-provider"
    }

    // JSON
    implementation "com.fasterxml.jackson.core:jackson-databind:$ver.jackson"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$ver.jackson"
    implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$ver.jackson"

    // DI
    implementation "com.google.inject:guice:$ver.guice"
    implementation "com.google.inject.extensions:guice-servlet:$ver.guice"

    // Scheduler
    implementation "de.skuzzle.inject:guice-async-extension:1.0.0"

    // Database
    implementation "org.xerial:sqlite-jdbc:$ver.sqlite"
    implementation "com.zaxxer:HikariCP:$ver.hikaricp"
    implementation "org.flywaydb:flyway-core:$ver.flyway"

    // File
    implementation "commons-io:commons-io:$ver.iocommons"

    // Security
    implementation "org.mindrot:jbcrypt:$ver.jbcrypt"

    // Logging
    runtimeOnly "ch.qos.logback:logback-classic:$ver.logback"
    runtimeOnly "org.slf4j:log4j-over-slf4j:$ver.slf4j"
    implementation "org.slf4j:jul-to-slf4j:$ver.slf4j"

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter-api:$ver.junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$ver.junit"
    // For Junit - Intellij compatibility
    testImplementation 'org.apiguardian:apiguardian-api:1.0.0'
    testImplementation "org.jboss.resteasy:resteasy-jackson2-provider:$ver.resteasy"
    // Database
    // Migrations
    testImplementation "org.flywaydb:flyway-core:$ver.flyway"
    // For Flyway's single db connection datasource
    testImplementation "org.springframework:spring-jdbc:$ver.springJdbc"
}
