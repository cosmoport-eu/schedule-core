buildscript {
    ext {
        ver = [
                junitPlugin   : '1.0.0-M4',
                versionsPlugin: '0.15.0'
        ]
    }

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }

    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:$ver.junitPlugin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$ver.versionsPlugin"
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.github.ben-manes.versions'

version = '0.1.1'

repositories {
    jcenter()
}

compileJava {
    options.compilerArgs << '-Xlint:all'
    options.warnings = true
    options.deprecation = true
    options.encoding = 'utf-8'
    options.incremental = true
}

mainClassName = 'com.cosmoport.core.App'

applicationDefaultJvmArgs = ["-Xms512m", "-Xmx1024m"]

distributions {
    main {
        contents {
            from(file("$projectDir/db")) {
                into 'db'
                include('**/*.db')
                includeEmptyDirs = false
            }
            from(file("$buildDir/scripts")) {
                include '*.bat'
            }
        }
    }
}
startScripts.applicationName = 'core0'
startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replace('APP_HOME=%DIRNAME%..', 'APP_HOME=%DIRNAME%.')
    }
}

ext {
    ver = [
            resteasy    : '3.1.3.Final',
            guice       : '4.1.0',
            jackson     : '2.9.0.pr3',
            sqlite      : '3.18.0',
            hikaricp    : '2.6.2',
            flyway      : '4.0.3',
            logback     : '1.2.3',
            slf4j       : '1.7.25',
            log4j       : '2.8.2',
            junitJupiter: '5.0.0-M4',
            springJdbc  : '4.3.8.RELEASE',
            palomino    : '1.2.1',
            jetty       : '9.4.5.v20170502',
            jbcrypt     : '0.4',
            iocommons   : '2.5',
            websocketApi: '1.1'
    ]
}

dependencies {
    // Server
    compile("com.palominolabs.http:jetty-http-server-wrapper:$ver.palomino") {
        exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
    }
    compile "org.eclipse.jetty:jetty-servlet:$ver.jetty"
    compile "org.eclipse.jetty.websocket:websocket-api:$ver.jetty"
    compile "org.eclipse.jetty.websocket:websocket-server:$ver.jetty"
    compile "org.eclipse.jetty.websocket:websocket-client:$ver.jetty"
    compile "javax.websocket:javax.websocket-api:$ver.websocketApi"

    // JAX-RS
    compile "org.jboss.resteasy:resteasy-jaxrs:$ver.resteasy"
    compile("org.jboss.resteasy:resteasy-guice:$ver.resteasy") {
        exclude group: "org.jboss.resteasy", module: "resteasy-client"
    }
    compile "org.jboss.resteasy:resteasy-multipart-provider:$ver.resteasy"

    // JSON
    compile "com.fasterxml.jackson.core:jackson-databind:$ver.jackson"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$ver.jackson"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$ver.jackson"

    // DI
    compile "com.google.inject:guice:$ver.guice"
    compile "com.google.inject.extensions:guice-servlet:$ver.guice"

    // Scheduler
    compile "de.skuzzle.inject:guice-async-extension:1.0.0"

    // Database
    // Main database
    compile "org.xerial:sqlite-jdbc:$ver.sqlite"
    // Database pool
    compile "com.zaxxer:HikariCP:$ver.hikaricp"
    // Database migrations tool
    compile "org.flywaydb:flyway-core:$ver.flyway"

    // File
    compile "commons-io:commons-io:$ver.iocommons"

    // Security
    // Password hashing
    compile "org.mindrot:jbcrypt:$ver.jbcrypt"

    // Logging
    runtime "ch.qos.logback:logback-classic:$ver.logback"
    runtime "org.slf4j:log4j-over-slf4j:$ver.slf4j"
    compile "org.slf4j:jul-to-slf4j:$ver.slf4j"

    // Test
    compile "org.springframework:spring-jdbc:$ver.springJdbc"
    testCompile "org.junit.jupiter:junit-jupiter-api:$ver.junitJupiter"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$ver.junitJupiter"
    testCompile "org.jboss.resteasy:resteasy-jackson2-provider:$ver.resteasy"
    testCompile "org.apache.logging.log4j:log4j-core:$ver.log4j"
    testCompile "org.apache.logging.log4j:log4j-jul:$ver.log4j"
}