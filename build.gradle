buildscript {
    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.1.0-M1"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.17.0"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

version = '0.1.3'
wrapper.gradleVersion = '4.5'

repositories {
    mavenCentral()
    jcenter()
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    options.compilerArgs << '-Xlint:-processing' << '-Xlint:all'
    options.warnings = true
    options.deprecation = true
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

mainClassName = 'com.cosmoport.core.App'

applicationDefaultJvmArgs = ["-Xms512m", "-Xmx1024m"]

distributions {
    main {
        contents {
            from(file("$projectDir/db")) {
                into 'db'
                include('**/*.db')
                includeEmptyDirs = false
            }
            from(file("$buildDir/scripts")) {
                include '*.bat'
            }
        }
    }
}
startScripts.applicationName = 'core0'
startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replace('APP_HOME=%DIRNAME%..', 'APP_HOME=%DIRNAME%.')
    }
}

ext {
    ver = [
            resteasy     : '4.0.0.Beta1',
            guice        : '4.1.0',
            jackson      : '2.9.3',
            sqlite       : '3.21.0.1',
            hikaricp     : '2.7.6',
            flyway       : '5.0.6',
            logback      : '1.2.3',
            slf4j        : '1.7.22',
            log4j        : '2.10.0',
            junitJupiter : '5.1.0-M1',
            junitPlatform: '1.1.0-M1',
            springJdbc   : '5.0.2.RELEASE',
            palomino     : '1.2.1',
            jetty        : '9.4.8.v20171121',
            jbcrypt      : '0.4',
            iocommons    : '2.6',
            websocketApi : '1.1'
    ]
}

dependencies {
    // Server
    compile("com.palominolabs.http:jetty-http-server-wrapper:$ver.palomino") {
        exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
    }
    compile "org.eclipse.jetty:jetty-servlet:$ver.jetty"

    compile "org.eclipse.jetty.websocket:websocket-api:$ver.jetty"
    compile "org.eclipse.jetty.websocket:websocket-server:$ver.jetty"
    compile "org.eclipse.jetty.websocket:websocket-client:$ver.jetty"
    compile "javax.websocket:javax.websocket-api:$ver.websocketApi"

    // JAX-RS
    compile "org.jboss.resteasy:resteasy-jaxrs:$ver.resteasy"
    compile("org.jboss.resteasy:resteasy-guice:$ver.resteasy") {
        exclude group: "org.jboss.resteasy", module: "resteasy-client"
    }
    compile("org.jboss.resteasy:resteasy-multipart-provider:$ver.resteasy") {
        exclude group: "org.jboss.resteasy", module: "resteasy-jaxb-provider"
    }

    // JSON
    compile "com.fasterxml.jackson.core:jackson-databind:$ver.jackson"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$ver.jackson"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$ver.jackson"

    // DI
    compile "com.google.inject:guice:$ver.guice"
    compile "com.google.inject.extensions:guice-servlet:$ver.guice"

    // Scheduler
    compile "de.skuzzle.inject:guice-async-extension:1.0.0"

    // Database
    compile "org.xerial:sqlite-jdbc:$ver.sqlite"
    compile "com.zaxxer:HikariCP:$ver.hikaricp"
    compile "org.flywaydb:flyway-core:$ver.flyway"

    // File
    compile "commons-io:commons-io:$ver.iocommons"

    // Security
    compile "org.mindrot:jbcrypt:$ver.jbcrypt"

    // Logging
    runtime "ch.qos.logback:logback-classic:$ver.logback"
    runtime "org.slf4j:log4j-over-slf4j:$ver.slf4j"
    compile "org.slf4j:jul-to-slf4j:$ver.slf4j"

    // Test
    testCompile "org.junit.jupiter:junit-jupiter-api:$ver.junitJupiter"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$ver.junitJupiter"
    testCompile "org.junit.platform:junit-platform-launcher:$ver.junitPlatform"
    // For Junit - Intellij compatibility
    testCompile 'org.apiguardian:apiguardian-api:1.0.0'
    testCompile "org.jboss.resteasy:resteasy-jackson2-provider:$ver.resteasy"
    // Database
    // Migrations
    testCompile "org.flywaydb:flyway-core:$ver.flyway"
    // For Flyway's single db connection datasource
    testCompile "org.springframework:spring-jdbc:$ver.springJdbc"
}
